plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs'
    id 'org.jetbrains.kotlin.plugin.compose'
}

def getProperty(String filename, String propName) {
    def propsFile = rootProject.file(filename)
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props[propName] != null) {
            return props[propName]
        } else {
            print("No such property " + propName + " in file " + filename)
        }
    } else {
        print(filename + " does not exist!")
    }
}

android {
    compileSdk 36

    defaultConfig {
        applicationId "com.mattar.nyt_top_stories"
        targetSdk 36
        minSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "nyt_api_key", "\"${getProperty("local.properties", "nyt_api_key")}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
        compose true
    }
    namespace 'com.mattar.nyt_top_stories'
}

composeCompiler {
    reportsDestination = layout.buildDirectory.dir("compose_compiler")
    stabilityConfigurationFile = rootProject.layout.projectDirectory.file("stability_config.conf")
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.2'

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"

    //Jetpack Compose
    def composeBom = platform('androidx.compose:compose-bom:2025.07.00')
    implementation composeBom
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui'

    implementation 'androidx.activity:activity-compose:1.10.1'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.9.2'

    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    implementation 'androidx.compose.material:material-icons-extended'

    implementation 'androidx.compose.material3.adaptive:adaptive'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation("io.coil-kt:coil:2.7.0")

    implementation "com.squareup.okhttp3:okhttp:5.1.0"
    implementation "com.squareup.okhttp3:logging-interceptor:5.1.0"
    implementation "com.squareup.retrofit2:retrofit:3.0.0"
    api("com.squareup.moshi:moshi-kotlin:1.15.2")
    api("com.squareup.moshi:moshi-adapters:1.15.2")
    api('com.squareup.retrofit2:converter-moshi:3.0.0')

    implementation "com.airbnb.android:lottie:6.6.7"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    testImplementation "io.mockk:mockk:1.14.5"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2'
    testImplementation "androidx.arch.core:core-testing:2.2.0"

    androidTestImplementation composeBom
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}

kapt {
    correctErrorTypes true
}